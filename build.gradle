import net.fabricmc.loom.task.RemapJar

plugins {
    id 'fabric-loom' version '0.2.1-SNAPSHOT'
    id "com.github.johnrengelman.shadow" version "4.0.3"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = "RoughlyEnoughItems"
version = project.mod_version

def includeDep = true

minecraft {
}

repositories {
    maven { url "https://tehnut.info/maven/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://minecraft.curseforge.com/api/maven" }
}

processResources {
    filesMatching('fabric.mod.json') {
        expand 'version': project.version
    }
    inputs.property "version", project.version
}

configurations {
    included {
        transitive = false
    }
}

shadowJar {
    version = project.mod_version.replaceAll('\\+build.', '.')
    classifier = includeDep ? null : "bare"
    configurations = [project.configurations.included]
}

task remapShadowJar(type: RemapJar) {
    jar = shadowJar.archivePath
}
tasks.remapShadowJar.dependsOn tasks.shadowJar

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_version}"
    modCompile "net.fabricmc:fabric-loader:${project.fabricloader_version}"
    modCompile "net.fabricmc:fabric:${project.fabric_version}"
    modCompile "cloth:ClothEvents:${cloth_events_version}"
    modCompile "cloth-config:ClothConfig:${cloth_config_version}"
    if (includeDep) {
        include "net.fabricmc:fabric:${project.fabric_version}"
        include "cloth:ClothEvents:${cloth_events_version}"
        include "cloth-config:ClothConfig:${cloth_config_version}"
    }

    compile "blue.endless:jankson:${project.jankson_version}"
    included "blue.endless:jankson:${project.jankson_version}"

    modCompile "io.github.prospector.modmenu:ModMenu:${modmenu_version}"
}
