plugins {
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

import java.util.zip.ZipFile

archivesBaseName = rootProject.name + "-plugin-compatibilities-forge"

configurations {
    runtimeDependencies
    compileClasspath.extendsFrom runtimeDependencies
    runtimeClasspath.extendsFrom runtimeDependencies
    shadowed // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
}

dependencies {
    forge("net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}")
    runtimeDependencies(project(path: ":jei-compatibility-layer", configuration: "namedElements")) { transitive false }
    shadowed(project(path: ":jei-compatibility-layer", configuration: "namedElements")) { transitive false }
}

loom {
    forge {
        mixinConfig "rei-jei-internals-workaround.mixins.json"
    }
}

shadowJar {
    configurations = [project.configurations.shadowed]
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    dependsOn project(":forge").tasks.shadowJar
    classifier null
    classpath.from project(":forge").tasks.shadowJar.archiveFile.get()
}

java {
    withSourcesJar()
}

sourcesJar {
    afterEvaluate {
        def depSources = project(":jei-compatibility-layer").sourcesJar
        dependsOn depSources
        from depSources.archiveFile.map { zipTree(it) }
    }
}

processResources {
    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
    inputs.property "version", project.version
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenREIPluginCompatibilities(MavenPublication) {
            artifactId = rootProject.name + "-plugin-compatibilities-forge"
            from components.java
        }
    }
}

unifiedPublishing {
    project {
        displayName = "[Forge $rootProject.supported_version] v$project.version"
        releaseType = "release"
        gameVersions = ["1.19"]
        gameLoaders = ["forge"]
        changelog = rootProject.releaseChangelog

        mainPublication remapJar

        if (project.hasProperty("danielshe_curse_api_key") || System.getenv("danielshe_curse_api_key") != null) {
            curseforge {
                token = project.hasProperty("danielshe_curse_api_key") ? project.property("danielshe_curse_api_key") : System.getenv("danielshe_curse_api_key")
                id = "521393"
                gameVersions.addAll "Java 17"
            }
        }

        if (project.hasProperty("modrinth_key") || System.getenv("modrinth_key") != null) {
            modrinth {
                token = project.hasProperty("modrinth_key") ? project.property("modrinth_key") : System.getenv("modrinth_key")
                id = "1PfY6b5p"
                version = "$project.version+$project.name"
            }
        }
    }
}

task updateJEIAssets {
    doLast {
        File jeiZip = file("build/jei.zip")
        jeiZip.delete()
        jeiZip.parentFile.mkdirs()
        jeiZip.withOutputStream { out ->
            new URL("https://github.com/mezz/JustEnoughItems/archive/refs/heads/1.19.zip").withInputStream { from -> out << from; }
        }
        new ZipFile(jeiZip).withCloseable { ZipFile zip ->
            zip.stream().forEach { entry ->
                String path = entry.name.substring(entry.name.indexOf('/') + 1)
                if (path.startsWith("Common/src/main/resources/assets") && !entry.isDirectory()) {
                    File newFile = file(path.replaceFirst("Common/", ""))
                    newFile.delete()
                    newFile.parentFile.mkdirs()
                    newFile.withOutputStream { out ->
                        out << zip.getInputStream(entry)
                    }
                }
            }
        }
    }
}
